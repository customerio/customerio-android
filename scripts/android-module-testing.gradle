// Gradle configuration to setup automated JVM and Android instrumentation testing.
// In this file, include testing tools and configuration that is used by all modules.

import io.customer.android.Dependencies

android {
    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests {
            // From: http://robolectric.org/getting-started/
            includeAndroidResources = true
        }
    }
}

dependencies {
    testImplementation project(':common-test')
    androidTestImplementation project(':common-test')

    testImplementation Dependencies.coroutinesTest
    testImplementation Dependencies.junitJupiter
    testImplementation Dependencies.mockK
    testImplementation Dependencies.okhttpMockWebserver
    testImplementation Dependencies.robolectric

    // Enables running JUnit 4 tests within the JUnit 5 (jupiter) platform.
    testRuntimeOnly Dependencies.junitVintageEngine

    androidTestImplementation Dependencies.androidxTestRunner
    androidTestImplementation Dependencies.androidxTestRules
    androidTestImplementation Dependencies.espressoCore
    androidTestImplementation Dependencies.espressoIntents
    androidTestImplementation Dependencies.mockKAndroid
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    filter {
        // Allows tests inheriting from RobolectricTest to be excluded to run with JUnit4
        excludeTestsMatching("*Robolectric*")
    }
    ignoreFailures = true
    // https://github.com/mockk/mockk/issues/681
    jvmArgs("--add-opens", "java.base/java.time=ALL-UNNAMED",
            "--add-opens", "java.base/java.util=ALL-UNNAMED"
    )
}
