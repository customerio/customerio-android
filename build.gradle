import io.customer.android.Dependencies

apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply plugin: 'binary-compatibility-validator'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'

buildscript {

    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    }
    dependencies {
        classpath Dependencies.androidGradlePlugin
        classpath Dependencies.kotlinGradlePlugin
        classpath Dependencies.kotlinSerialization
        classpath Dependencies.kotlinBinaryValidator
        classpath Dependencies.gradleNexusPublishPlugin
        classpath Dependencies.dokka
        classpath Dependencies.composeCompilerGradlePlugin

        // if you have a Gradle plugin dependency in the classpath of the root project,
        // it will not be added to all modules even if they don't need it.
        // Gradle will only add the plugin to modules that explicitly declare a dependency on it.
        classpath Dependencies.googleServicesPlugin
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apiValidation {
    ignoredPackages += [
            'io/customer/base/extenstions',
            'io/customer/sdk/api',
            'io/customer/sdk/data/store',
            'io/customer/sdk/queue',
            'io/customer/sdk/extensions',
            'io/customer/sdk/util',
    ]

    ignoredProjects += [
            'common-test',
            'base',
            'kotlin_compose',
            'java_layout',
    ]

    nonPublicMarkers += [
            'io.customer.base.internal.InternalCustomerIOApi',
    ]

    /**
     * Classes (fully qualified) that are excluded from public API dumps even if they
     * contain public API.
     */
    ignoredClasses.add("io.customer.messagingpush.BuildConfig")
    ignoredClasses.add("io.customer.messaginginapp.BuildConfig")
    ignoredClasses.add("io.customer.messaginginapp.compose.BuildConfig")
    ignoredClasses.add("io.customer.sdk.BuildConfig")
}

apply from: "${rootDir}/scripts/publish-root.gradle"
apply from: "${rootDir}/scripts/sdk-binary-size.gradle"

// Documentation generation
tasks.register('generateDocs') {
    dependsOn 'dokkaHtmlMultiModule'
    group = 'documentation'
    description = 'Generates aggregated HTML reference documentation for all SDK modules'
    
    doLast {
        def docsDir = file("${rootDir}/build/dokka/htmlMultiModule")
        if (docsDir.exists()) {
            println "\n‚úÖ Documentation generated successfully!"
            println "üìÑ Location: ${docsDir.absolutePath}"
            println "üåê Open: file://${docsDir.absolutePath}/index.html"
        }
    }
}

// Configure Dokka for better output and to exclude internal APIs
tasks.withType(org.jetbrains.dokka.gradle.DokkaMultiModuleTask).configureEach {
    moduleName.set("Customer.io Android SDK")
    
    // Add custom documentation pages to navigation
    includes.from("${rootDir}/ref-docs.md")
    
    // Add custom logo and styling
    // Note: Paths must be absolute for Dokka to find them during generation
    pluginsMapConfiguration.set([
        "org.jetbrains.dokka.base.DokkaBase": """{ 
            "customAssets": ["${file("${rootDir}/.dokka/logo.png").absolutePath.replace('\\', '/')}"],
            "customStyleSheets": ["${file("${rootDir}/.dokka/custom-styles.css").absolutePath.replace('\\', '/')}"]
        }"""
    ])
}

// Configure each module's Dokka task to exclude internal APIs
subprojects {
    tasks.withType(org.jetbrains.dokka.gradle.DokkaTaskPartial).configureEach {
        dokkaSourceSets.configureEach {
            // Only document public APIs (excludes internal, private, protected)
            includeNonPublic.set(false)
            
            // Skip deprecated APIs (set to false if you want to document them)
            skipDeprecated.set(false)
            
            // Skip empty packages
            skipEmptyPackages.set(true)
            
            // Suppress all packages named '*.internal.*'
            perPackageOption {
                matchingRegex.set(".*\\.internal.*")
                suppress.set(true)
            }
            
            // Add module-specific documentation if it exists
            def moduleDocsFile = file("${projectDir}/MODULE.md")
            if (moduleDocsFile.exists()) {
                includes.from(moduleDocsFile)
            }
            
            // Link to Android SDK documentation
            externalDocumentationLink {
                url.set(new URL("https://developer.android.com/reference/"))
            }
            
            // Link to Kotlin stdlib
            externalDocumentationLink {
                url.set(new URL("https://kotlinlang.org/api/latest/jvm/stdlib/"))
            }
        }
    }
}