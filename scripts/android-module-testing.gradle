// Gradle configuration to setup automated JVM and Android instrumentation testing.
// In this file, include testing tools and configuration that is used by all modules.

import io.customer.android.Dependencies

android {
    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests {
            // From: http://robolectric.org/getting-started/
            includeAndroidResources = true
        }
    }

    sourceSets {
        // Preferred to put test classes in `src/sharedTest/` so the tests will run on both
        // JVM (for speed) and Android (for confidence).
        String sharedTestDir = 'src/sharedTest/java'
        test.java.srcDirs += sharedTestDir
        androidTest.java.srcDirs += sharedTestDir
    }
}

dependencies {
    testImplementation project(':common-test')
    androidTestImplementation project(':common-test')

    testImplementation Dependencies.coroutinesTest
    // compile time bug when trying to run Android tests: https://github.com/Kotlin/kotlinx.coroutines/issues/2023
    // Solution from: https://github.com/Kotlin/kotlinx.coroutines/tree/master/kotlinx-coroutines-debug#debug-agent-and-android
    androidTestImplementation(Dependencies.coroutinesTest) {
        exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
    }

    testImplementation Dependencies.androidxTestJunit
    androidTestImplementation Dependencies.androidxTestJunit
    androidTestImplementation Dependencies.androidxTestRunner
    androidTestImplementation Dependencies.androidxTestRules

    testImplementation Dependencies.mockito
    androidTestImplementation Dependencies.mockitoAndroid
    testImplementation Dependencies.mockitoKotlin
    androidTestImplementation Dependencies.mockitoKotlin
    testImplementation Dependencies.robolectric

    testImplementation Dependencies.kluent
    androidTestImplementation Dependencies.kluent
    testImplementation Dependencies.okhttpMockWebserver

    testImplementation Dependencies.junit4
    androidTestImplementation Dependencies.junit4
}
