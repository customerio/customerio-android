import io.customer.android.Configurations
import io.customer.android.Dependencies

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.twilio.apkscale'
}

ext {
    PUBLISH_GROUP_ID = Configurations.artifactGroup
    PUBLISH_ARTIFACT_ID = "messaging-push-fcm"
}

apply from: "${rootDir}/scripts/publish-module.gradle"
apply from: "${rootDir}/scripts/android-config.gradle"
apply from: "${rootDir}/scripts/codecov-android.gradle"
apply from: "${rootDir}/scripts/android-module-testing.gradle"

android {
    namespace 'io.customer.messagingpush'
    defaultConfig {
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api project(":base")
    api project(":core")

    implementation Dependencies.coroutinesCore
    implementation Dependencies.coroutinesAndroid
    implementation Dependencies.retrofit
    implementation Dependencies.retrofitMoshiConverter
    implementation Dependencies.okhttpLoggingInterceptor

    api Dependencies.firebaseMessaging
    implementation Dependencies.googlePlayServicesBase

    // MockK dependencies for testing
    testImplementation(Dependencies.mockK)
    testImplementation(Dependencies.coroutinesTest)
    // JUnit 5 dependencies for testing
    testImplementation(platform(Dependencies.junitBom))
    testImplementation(Dependencies.junitJupiter)
    // Enables running JUnit 4 tests within the JUnit 5 (jupiter) platform.
    testRuntimeOnly(Dependencies.junitVintageEngine)
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    filter {
        excludeTestsMatching("*Integration*")
    }
    ignoreFailures = true
    // https://github.com/mockk/mockk/issues/681
    jvmArgs("--add-opens", "java.base/java.time=ALL-UNNAMED",
            "--add-opens", "java.base/java.util=ALL-UNNAMED"
    )
}