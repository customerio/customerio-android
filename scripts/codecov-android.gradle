apply from: "${rootDir}/scripts/codecov-common.gradle"


// make sure name of task is identical for java and Android so CI script can use the same name for running both. 
task runJacocoTestReport(type: JacocoReport) {
    dependsOn 'testDebugUnitTest' // run tests and then generate report 

    reports {
        xml.required = true 
        html.required = true 
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    // def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(files("${buildDir}/jacoco/testDebugUnitTest.exec"))
}

android {
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }
}