apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                // to speed up local development build times, skip dokka
                if (!IS_DEVELOPMENT) {
                    artifact androidSourcesJar
                    artifact javadocJar
                }

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Customer.io Android SDK'
                    url = 'https://github.com/customerio/customerio-android'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/customerio/customerio-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = "customerio"
                            name = "Customer.io Team"
                            email = "win@customer.io"
                        }
                    }
                    scm {
                        url = "https://github.com/customerio/customerio-android"
                        connection = "scm:git@github.com:customerio/customerio-android.git"
                        developerConnection = "scm:git@github.com:customerio/customerio-android.git"
                    }
                }
            }
        }
    }

    // Ensure metadata generation depends on sources jar
    tasks.matching { it.name.startsWith("generateMetadataFileFor") }.configureEach {
        dependsOn(tasks.named("androidSourcesJar"))
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    if (!IS_DEVELOPMENT) // skip signing for local installs during development
        sign publishing.publications
}