import io.customer.android.Configurations
import io.customer.android.Dependencies

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.twilio.apkscale'
}

ext {
    PUBLISH_GROUP_ID = Configurations.artifactGroup
    PUBLISH_ARTIFACT_ID = "tracking"
}

apply from: "${rootDir}/scripts/publish-module.gradle"
apply from: "${rootDir}/scripts/android-config.gradle"
apply from: "${rootDir}/scripts/codecov-android.gradle"
apply from: "${rootDir}/scripts/android-module-testing.gradle"

android {
    namespace 'io.customer.tracking'
    defaultConfig {
        consumerProguardFiles "consumer-rules.pro"
    }
    buildFeatures {
        // BuildConfig is required currently to differentiate between debug and release environments in the SDK
        buildConfig = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest {
            java.srcDirs += 'src/sharedTest/java'
        }
        test {
            java.srcDirs += 'src/sharedTest/java'
        }
    }
}

dependencies {
    api project(":base")
    api project(":core")

    implementation Dependencies.coroutinesCore
    implementation Dependencies.coroutinesAndroid
    implementation Dependencies.retrofit
    implementation Dependencies.moshi
    implementation Dependencies.retrofitMoshiConverter
    implementation Dependencies.okhttpLoggingInterceptor
    api Dependencies.androidxCoreKtx

    kapt(Dependencies.moshiCodeGen)
    kaptTest(Dependencies.moshiCodeGen)
    kaptAndroidTest(Dependencies.moshiCodeGen)

    // Dependencies for tests that were removed from common-test module
    // Adding them here to avoid breaking the tests in tracking module
    testImplementation Dependencies.mockito
    testImplementation Dependencies.mockitoKotlin
    androidTestImplementation Dependencies.coroutinesTest
    androidTestImplementation Dependencies.mockito
    androidTestImplementation Dependencies.mockitoKotlin
    androidTestImplementation Dependencies.okhttpMockWebserver
}
