import io.customer.android.Configurations
import io.customer.android.Dependencies

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id "org.jetbrains.kotlin.kapt"
}

ext {
    PUBLISH_GROUP_ID = Configurations.artifactGroup
    PUBLISH_ARTIFACT_ID = "tracking"
}

apply from: "${rootDir}/scripts/publish-module.gradle"
apply from: "${rootDir}/scripts/android-config.gradle"

android {

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests {
            // From: http://robolectric.org/getting-started/
            includeAndroidResources = true
        }
    }

    sourceSets {
        // Put code into below directory that you want shared between JVM and Android instrumentation tests.
        String sharedTestDir = 'src/sharedTest/java'
        test.java.srcDirs += sharedTestDir
        androidTest.java.srcDirs += sharedTestDir
    }
}

dependencies {
    api project(":base")

    implementation Dependencies.coroutinesCore
    implementation Dependencies.coroutinesAndroid
    implementation Dependencies.retrofit
    implementation Dependencies.moshi
    implementation Dependencies.retrofitMoshiConverter
    implementation Dependencies.okhttpLoggingInterceptor

    kapt(Dependencies.moshiCodeGen)
    kaptTest(Dependencies.moshiCodeGen)
    kaptAndroidTest(Dependencies.moshiCodeGen)

    testImplementation Dependencies.androidxTestJunit
    androidTestImplementation Dependencies.androidxTestJunit
    androidTestImplementation Dependencies.androidxTestRunner
    androidTestImplementation Dependencies.androidxTestRules

    testImplementation Dependencies.mockito
    androidTestImplementation Dependencies.mockitoAndroid
    testImplementation Dependencies.mockitoKotlin
    androidTestImplementation Dependencies.mockitoKotlin
    testImplementation Dependencies.robolectric

    testImplementation Dependencies.kluent
    androidTestImplementation Dependencies.kluent
    testImplementation Dependencies.okhttpMockWebserver

    testImplementation Dependencies.junit4
    androidTestImplementation Dependencies.junit4
}

