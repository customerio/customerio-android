import io.customer.android.Configurations
import io.customer.android.Dependencies

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.twilio.apkscale'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

ext {
    PUBLISH_GROUP_ID = Configurations.artifactGroup
    PUBLISH_ARTIFACT_ID = "datapipelines"
}

apply from: "${rootDir}/scripts/publish-module.gradle"
apply from: "${rootDir}/scripts/android-config.gradle"
apply from: "${rootDir}/scripts/codecov-android.gradle"
apply from: "${rootDir}/scripts/android-module-testing.gradle"

android {
    namespace 'io.customer.datapipelines'
    defaultConfig {
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api project(":base")
    api project(":core")
    implementation(Dependencies.segment)
    // Use this as API so customers can provide objects serializations without
    // needing to add it as a dependency to their app
    api(Dependencies.kotlinxSerializationJson)

    testImplementation(Dependencies.mockito)
    testImplementation(Dependencies.mockitoKotlin)
    testImplementation(Dependencies.robolectric)

    testImplementation(Dependencies.mockK)
    testImplementation(Dependencies.coroutinesTest)

    testImplementation(platform(Dependencies.junitBom))
    testImplementation(Dependencies.junitJupiter)

    // enables running JUnit 4 tests within the JUnit 5 (jupiter) platform.
    testRuntimeOnly(Dependencies.junitVintageEngine)
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    filter {
        excludeTestsMatching("*Integration*")
    }
    ignoreFailures = true
    // https://github.com/mockk/mockk/issues/681
    jvmArgs("--add-opens", "java.base/java.time=ALL-UNNAMED",
            "--add-opens", "java.base/java.util=ALL-UNNAMED"
    )
}
