import io.customer.android.Configurations
import io.customer.android.Dependencies

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.twilio.apkscale'
}

ext {
    PUBLISH_GROUP_ID = Configurations.artifactGroup
    PUBLISH_ARTIFACT_ID = "messaging-in-app"
}

apply from: "${rootDir}/scripts/publish-module.gradle"
apply from: "${rootDir}/scripts/android-config.gradle"
apply from: "${rootDir}/scripts/codecov-android.gradle"
apply from: "${rootDir}/scripts/android-module-testing.gradle"

android {
    namespace 'io.customer.messaginginapp'
    defaultConfig {
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    api project(":base")
    api project(":core")

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"

    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    implementation Dependencies.coroutinesCore
    implementation Dependencies.coroutinesAndroid
    implementation Dependencies.androidxAppCompat
    implementation Dependencies.retrofit
    implementation Dependencies.okhttpLoggingInterceptor

    // MockK dependencies for testing
    testImplementation(Dependencies.mockk)
    testImplementation(Dependencies.coroutinesTest)
    // JUnit 5 dependencies for testing
    testImplementation(platform(Dependencies.junitBom))
    testImplementation(Dependencies.junitJupiter)
    // Enables running JUnit 4 tests within the JUnit 5 (jupiter) platform.
    testRuntimeOnly(Dependencies.junitVintageEngine)
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    filter {
        excludeTestsMatching("*Integration*")
    }
    ignoreFailures = true
    // https://github.com/mockk/mockk/issues/681
    jvmArgs("--add-opens", "java.base/java.time=ALL-UNNAMED",
            "--add-opens", "java.base/java.util=ALL-UNNAMED"
    )
}
